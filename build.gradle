buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.19.0'
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.5.20"
    id "com.diffplug.spotless" version "5.14.0"
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply from: "dependencies.gradle"
apply plugin: "com.vanniktech.maven.publish"

allprojects {
    plugins.withId("com.vanniktech.maven.publish") {
        mavenPublish {
            sonatypeHost = "S01"
        }
    }
}

group releasing.group
version releasing.version

repositories {
    mavenCentral()
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin/'
    test.java.srcDirs += 'src/test/kotlin/'
}

spotless {
    kotlin {
        ktfmt(versions.ktfmt.toString()).dropboxStyle()
    }
}

dependencies {
    // Kotlin
    implementation deps.kotlin.stdlib
    implementation deps.kotlin.reflect
    // Clikt
    implementation deps.clikt
    // JDOM
    implementation deps.jdom
    // Dagger
    implementation deps.dagger.dagger
    kapt deps.dagger.compiler
}

jar {
    manifest {
        attributes 'Main-Class': releasing.main_class
    }
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    archiveFileName.set(releasing.archive_name.toString())
}

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = versions.jvm_target
}
compileTestKotlin {
    kotlinOptions.jvmTarget = versions.jvm_target
}

rootProject.tasks.named("jar") {
    duplicatesStrategy = 'include'
}
